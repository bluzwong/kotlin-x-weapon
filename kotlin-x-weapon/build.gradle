
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'


buildscript {
    ext.kotlin_version = '0.12.1230'
    repositories {
        // mavenCentral()
        maven{ url 'http://maven.oschina.net/content/groups/public/'}
        jcenter {
            url "http://jcenter.bintray.com/"
        }
        maven  {
            url "http://maven.oschina.net/content/groups/public/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.android.tools.build:gradle:1.3.0'

    }
}
android {
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
  //  compile "org.jetbrains.kotlin:kotlin-stdlib:0.12.613"
   // compile "org.jetbrains.kotlin:kotlin-stdlib:0.12.1230"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.github.bluzwong'
version = '0.9.1'
def siteUrl = 'https://github.com/bluzwong/kotlin-x-weapon'
def gitUrl = 'https://github.com/bluzwong/kotlin-x-weapon'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'kotlin-x-weapon'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'bluzwong'
                        name 'zj Wong'
                        email 'brucewangzhijie@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'kotlin-x-weapon'
        desc = 'kotlin-x-weapon'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['kotlin', 'android', 'library']
        publicDownloadNumbers = true
    }
}
repositories {
    mavenCentral()
}